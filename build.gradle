plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.2.5'
    id 'jacoco'
}

group = 'org.theorea'
version = '1.0-SNAPSHOT'
sourceCompatibility = '21'
targetCompatibility = '21'

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = '1.6.2'
    lombokMapstructVersion = '0.2.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructVersion}"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    toolVersion = '10.3'
    showViolations = true
}
spotbugs {
    toolVersion = '4.8.5'
    effort = 'max'
    reportLevel = 'high'
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
bootJar {
    archiveFileName = 'Java_Notes.jar'
}